AC_INIT(igraph, @VERSION@, csardi@rmki.kfki.hu)
AC_CONFIG_SRCDIR(src/rinterface.c)
AC_CONFIG_HEADERS(src/config.h)

AC_PROG_CC

# Tricky check for C++ compiler, because Autoconf has a weird bug:
# http://lists.gnu.org/archive/html/autoconf/2006-03/msg00067.html
AC_PROG_CXX
AC_LANG_PUSH([C++])
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[
#include <iostream>
const char hw[] = "Hello, World\n";]],
                [[std::cout << hw;]])],
        [AC_PROG_CXXCPP
        cxx_error=no],
        [AC_MSG_ERROR([no C++ compiler found or it cannot create executables])])
AC_LANG_POP([C++])

AC_CHECK_FUNCS(fmemopen, [HAVE_FMEMOPEN=1], [HAVE_FMEMOPEN=0])
AC_SUBST(HAVE_FMEMOPEN)

AC_CHECK_FUNCS(open_memstream, [HAVE_OPEN_MEMSTREAM=1], [HAVE_OPEN_MEMSTREAM=0])
AC_SUBST(HAVE_OPEN_MEMSTREAM)

LIBS_SAVE=$LIBS
LIBS="$LIBS -lm"
AC_CHECK_FUNCS([rintf], [HAVE_RINTF=1], [HAVE_RINTF=0])
AC_CHECK_FUNCS([finite], [HAVE_FINITE=1], [HAVE_FINITE=0])
AC_CHECK_FUNCS([expm1], [HAVE_EXPM1=1], [HAVE_EMPM1=0])
AC_CHECK_FUNCS([rint], [HAVE_RINT=1], [HAVE_RINT=0])
AC_CHECK_FUNCS([log2], [HAVE_LOG2=1], [HAVE_LOG2=0])
AC_CHECK_FUNCS([logbl], [HAVE_LOGBL=1], [HAVE_LOGBL=0])
AC_CHECK_FUNCS([snprintf], [HAVE_SNPRINTF=1], [HAVE_SNPRINTF=0])
AC_CHECK_FUNCS([log1p], [HAVE_LOG1P=1], [HAVE_LOG1P=0])
AC_CHECK_FUNCS([round], [HAVE_ROUND=1], [HAVE_ROUND=0])
AC_CHECK_FUNCS([fmin], [HAVE_FMIN=1], [HAVE_FMIN=0])
LIBS=$LIBS_SAVE
AC_SUBST(HAVE_RINTF)
AC_SUBST(HAVE_FINITE)

AC_CHECK_HEADER([sys/times.h], 
      [AC_DEFINE([HAVE_TIMES_H], [1], [Define to 1 if you have the sys/times.h header])])

graphml_support=yes
AC_ARG_ENABLE(graphml,
              AC_HELP_STRING([--disable-graphml], [Disable support for GraphML format]),
              [graphml_support=$enableval], [graphml_support=yes])

HAVE_LIBXML=0
if test $graphml_support = yes; then
  AC_PATH_PROG([XML2CONFIG], [xml2-config], [none])
  if test "$XML2CONFIG" = "none"; then
    graphml_support=no
  else
    XML2_LIBS=`$XML2CONFIG --libs`
    XML2_CFLAGS=`$XML2CONFIG --cflags`
    AC_CHECK_LIB([xml2], [xmlSAXUserParseFile], [
      OLDCFLAGS=${CFLAGS}
      OLDCPPFLAGS=${CPPFLAGS}
      CFLAGS=${XML2_CFLAGS}
      CPPFLAGS=${XML2_CFLAGS}
      AC_CHECK_HEADER([libxml/parser.h], [
        HAVE_LIBXML=1
	AC_DEFINE([HAVE_LIBXML], [1], [Define to 1 if you have the libxml2 libraries installed])
        CFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	CPPFLAGS="${OLDCFLAGS} ${XML2_CFLAGS}"
	AC_SUBST(XML2_LIBS)
	AC_SUBST(XML2_CFLAGS)
      ], [
        graphml_support=no
        CFLAGS=${OLDCFLAGS}
	CPPFLAGS=${OLDCPPFLAGS}
      ])
    ], [
      graphml_support=no
    ])
  fi
fi
AC_SUBST(HAVE_LIBXML)

HAVE_GMP=0
GMP_LIBS=""
gmp_support=no
AC_ARG_ENABLE(gmp, AC_HELP_STRING([--disable-gmp], [Compile without the GMP library]))
if test "x$enable_gmp" != "xno"; then
  AC_CHECK_LIB([gmp], [__gmpz_add], [
    AC_CHECK_HEADER([gmp.h], [
      HAVE_GMP=1
      AC_DEFINE([HAVE_GMP], [1], [Define to 1 if you have the GMP library])
      gmp_support=yes
      GMP_LIBS="-lgmp"      
    ])
  ])
fi
AC_SUBST(HAVE_GMP)
AC_SUBST(GMP_LIBS)

AC_CONFIG_FILES([R/config.R src/Makevars])

AC_OUTPUT
