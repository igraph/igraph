# igraph-config.cmake
#
# igraph CMake package
#
# The following variables are set:
#
#   - IGRAPH_VERSION          - The igraph version string.
#   - IGRAPH_INTEGER_SIZE     - The integer size igraph was configured with (32 or 64).
#   - IGRAPH_GLPK_SUPPORT     - Whether igraph was compiled with GLPK support.
#   - IGRAPH_GRAPHML_SUPPORT  - Whether igraph was compiled with GraphML support.
#

set(IGRAPH_VERSION "@PACKAGE_VERSION_BASE@")
set(IGRAPH_INTEGER_SIZE @IGRAPH_INTEGER_SIZE@)
set(IGRAPH_GLPK_SUPPORT @IGRAPH_GLPK_SUPPORT@)
set(IGRAPH_GRAPHML_SUPPORT @IGRAPH_GRAPHML_SUPPORT@)
@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/igraph-targets.cmake")

# Check whether C++ support is enabled; this is needed to ensure that programs
# that are dependent on igraph will get linked with the C++ linker and not the
# "plain" C linker
get_property(LANGUAGES GLOBAL PROPERTY ENABLED_LANGUAGES)
if("CXX" IN_LIST LANGUAGES)
  # This is okay
else()
  message(FATAL_ERROR "Please enable C++ support in your project if you are linking to igraph.")
endif()

# Turn on CMP0012 because the following if() conditionals will use "ON" and
# "OFF" verbatim and they must be evaluated as booleans
cmake_policy(PUSH)
cmake_policy(SET CMP0012 NEW)
if(@IGRAPH_OPENMP_SUPPORT@)
  find_package(OpenMP)
endif()
cmake_policy(POP)

check_required_components(igraph)
